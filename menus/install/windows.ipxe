#!ipxe

# Microsoft Windows
# https://www.microsoft.com

# SET DEFAULT URL FOR BOOT
set win_mirror ${mirror_url}/win
# SET DEFAULT VERSION OF WINDOWS
# W10 for Windows 10, S2019 for Server 2019
set version_win W10


# DO NOT TOUCH BELOW HERE
goto ${menu} ||

:windows
set os Microsoft Windows
menu ${os} 
clear selection
item --gap Installers
item boot ${space} Load ${os} Installer...
item --gap Options:
item version_set ${space} Version [ ${version_win} ]
choose selection || goto windows_exit
goto ${selection}

:version_set
iseq ${version_win} W10 && set version_win S2019 || set version_win W10
goto windows

:boot
imgfree
kernel ${win_mirror}/wimboot
initrd -n bootmgr     ${win_mirror}/${version_win}/bootmgr       bootmgr ||
initrd -n bootmgr.efi ${win_mirror}/${version_win}/bootmgr.efi   bootmgr.efi ||      
initrd -n bcd         ${win_mirror}/${version_win}/boot/bcd      bcd ||
initrd -n bcd         ${win_mirror}/${version_win}/Boot/BCD      bcd ||
initrd -n boot.sdi    ${win_mirror}/${version_win}/boot/boot.sdi boot.sdi ||
initrd -n boot.sdi    ${win_mirror}/${version_win}/Boot/boot.sdi boot.sdi ||
initrd -n boot.wim    ${win_mirror}/${version_win}/sources/boot.wim boot.wim
boot

:windows_exit
exit 0
